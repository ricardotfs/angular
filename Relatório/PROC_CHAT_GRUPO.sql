
/*

			DECLARE @IDCONTA INT = 84
			DECLARE @DATAINICIO DATETIME = '2020-05-13 00:00:00'
			DECLARE @DATAFIM DATETIME = '2020-05-13 23:59:59'
			DECLARE @IDGRUPO VARCHAR(MAX) = '21,23'
			DECLARE @ATENDENTE VARCHAR(MAX) = '142,264,253,274,273,278,279,175,143,277,280,281,249,261,257,282,251,283,284,255,176,285,286,265,245,260,266,256,287,288,250,177,276,259,262,263,254,248,289,258,290'
			DECLARE @QTD_PAGINA INT = 10
			DECLARE @PAGINA INT = 0

			EXEC PROC_CHAT_GRUPO @IDCONTA,@DATAINICIO,@DATAFIM,@IDGRUPO,@ATENDENTE,@QTD_PAGINA,@PAGINA



*/


ALTER PROC PROC_CHAT_GRUPO 
(	
	 @IDCONTA  INT 
	,@DATAINICIO DATETIME
	,@DATAFIM DATETIME
	,@IDGRUPO VARCHAR(MAX)
	,@ATENDENTE VARCHAR(MAX)
	,@QTD_PAGINA INT 
	,@PAGINA INT 
)

AS 
	BEGIN
		
		SET @ATENDENTE =  '0,' + @ATENDENTE
		DECLARE @TIMEZONE VARCHAR(MAX)
		SELECT @TIMEZONE = T.UTCOFFSET FROM CONTA (NOLOCK) JOIN TIMEZONES T (NOLOCK) ON T.TIMEZONEID = CONTA.IDTIMEZONE WHERE CONTA.Id = @IDCONTA
		
		DECLARE @HOUR INT =  SUBSTRING(@TIMEZONE, 1, 3)

		;WITH CHAT_TEMP AS (SELECT *,ROW_NUMBER() OVER(ORDER BY IDGRUPOCHAT ASC) AS ROWNUMBER
		   FROM (
					SELECT
						SUM(1) ENTRANTES,
						SUM((CASE WHEN ISNULL(C.IDPROPRIETARIO,0) <> 0 THEN 1 ELSE 0 END)) RECEBIDOS,
						SUM((CASE WHEN ISNULL(C.IDPROPRIETARIO,0) = 0 THEN 1 ELSE 0 END)) ABONDONADOS,	
						(SELECT 
							AVG( DATEDIFF(SECOND,DATEADD(HOUR,@HOUR,CC.DATACRIACAO),ISNULL(DATEADD(HOUR,@HOUR,CC.DATACONCLUSAO),DATEADD(HOUR,@HOUR,CC.DATACRIACAO)))) TEMPO_MEDIO_ABONDONO 
							FROM CHAT CC(NOLOCK)
							WHERE 
							CC.IDCONTA = @IDCONTA 
							AND CC.DATACRIACAO BETWEEN @DATAINICIO AND @DATAFIM 
							AND CC.IDGRUPOCHAT = C.IDGRUPOCHAT 
							AND ISNULL(CC.IDPROPRIETARIO,0) = 0
							GROUP BY IDGRUPOCHAT
						) TEMPO_MEDIO_ABONDONO,
						SUM((CASE WHEN C.IDSTATUS = 6 THEN 1 ELSE 0 END)) OFFILINE,
						AVG(DATEDIFF(SECOND,DATEADD(HOUR,@HOUR,C.DATACRIACAO),COALESCE(DATEADD(HOUR,@HOUR,C.DATAINICIO), DATEADD(HOUR,@HOUR,C.DATACONCLUSAO), DATEADD(HOUR,@HOUR,C.DATAALTERACAO)))) TEMPO_MEDIO_ESPERA,
						(SELECT 
							AVG(DATEDIFF(SECOND,DATEADD(HOUR,@HOUR,CC.DATAINICIO),ISNULL(DATEADD(HOUR,@HOUR,CC.DATACONCLUSAO),DATEADD(HOUR,@HOUR,CC.DATAALTERACAO)))) TEMPO_MEDIO_ATENDIMENTO 
							FROM CHAT CC(NOLOCK)
							WHERE 
							CC.IDCONTA = @IDCONTA 
							AND CC.DATACRIACAO BETWEEN @DATAINICIO AND @DATAFIM 
							AND CC.IDGRUPOCHAT = C.IDGRUPOCHAT 
							AND ISNULL(CC.IDPROPRIETARIO,0) <> 0
							GROUP BY IDGRUPOCHAT
						) TEMPO_MEDIO_ATENDIMENTO,
						SUM((CASE WHEN ISNULL(C.IDPROPRIETARIO,0) <> 0 AND DATEDIFF(SECOND,DATEADD(HOUR,@HOUR,C.DATACRIACAO),ISNULL(DATEADD(HOUR,@HOUR,C.DATAINICIO),DATEADD(HOUR,@HOUR,C.DATACRIACAO))) <= 30 THEN 1 ELSE 0 END)) ATENDIMENTO_ATE_30_SEG, 
						C.IDGRUPOCHAT
					FROM CHAT C(NOLOCK)
					WHERE 
						C.IDCONTA = @IDCONTA 
						AND C.DATACRIACAO BETWEEN @DATAINICIO AND @DATAFIM 
						AND C.IDGRUPOCHAT IN(SELECT INTVALUE FROM [DBO].[CSVTOINT](@IDGRUPO)) 
						AND ISNULL(C.IDPROPRIETARIO,0) IN(SELECT INTVALUE FROM [DBO].[CSVTOINT](@ATENDENTE)) 					
				GROUP BY IDGRUPOCHAT
			  
			  ) AS TEMPO)
		
			SELECT TOP (@QTD_PAGINA) 
							Grupo = (SELECT G.NOME FROM GRUPOCHAT G WHERE G.ID = IDGRUPOCHAT),
							ENTRANTES [Quantidades de chats recebidos],
							RECEBIDOS [Quantidade de chat atendidos],
							CAST(ROUND(CAST(((CAST(RECEBIDOS AS DECIMAL)/CAST(ENTRANTES AS DECIMAL)) * 100) AS DECIMAL),2) AS VARCHAR) + '%' [%de chat recebidos x atendidos],
							ABONDONADOS [Chat perdidos],
							CAST(ROUND(CAST(((CAST(ABONDONADOS AS DECIMAL)/CAST(ENTRANTES AS DECIMAL)) * 100) AS decimal),2)  AS VARCHAR) + '%' [%Taxa de perdido],
							CONVERT(VARCHAR, DATEADD(SECOND, ISNULL(TEMPO_MEDIO_ABONDONO,0), 0), 108)[Tempo médio de perdido],
							OFFILINE [chat fora do horário de atendimento],
							CONVERT(VARCHAR, DATEADD(SECOND, ISNULL(TEMPO_MEDIO_ESPERA,0), 0), 108) [Tempo Médio de espera para ser atendido],
							CONVERT(VARCHAR, DATEADD(SECOND, ISNULL(TEMPO_MEDIO_ATENDIMENTO,0), 0), 108) [Tempo Médio de Atendimento],
							CAST(CAST((CAST(ATENDIMENTO_ATE_30_SEG AS DECIMAL)/CAST((CASE WHEN RECEBIDOS = 0 THEN 1 ELSE RECEBIDOS END) AS DECIMAL) * 100) AS DECIMAL) AS VARCHAR) + '%' [%Nível de serviço]
							--Total =(SELECT COUNT(1)FROM CHAT_TEMP)
			FROM CHAT_TEMP
			WHERE ROWNUMBER > (@PAGINA * @QTD_PAGINA)
	END
	