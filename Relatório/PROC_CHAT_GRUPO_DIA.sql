
/*
		DECLARE @IDCONTA INT = 84
		DECLARE @DATAINICIO DATETIME = '2020-04-07 00:00:00'
		DECLARE @DATAFIM DATETIME = '2020-05-07 23:59:59'
		DECLARE @IDGRUPO VARCHAR(MAX) = '23'
		DECLARE @ATENDENTE VARCHAR(MAX) = '142,264,253,274,273,278,279,175,143,277,280,281,249,261,257,282,251,283,284,255,176,285,286,265,245,260,266,256,287,288,250,177,276,259,262,263,254,248,289,258,290'
		DECLARE @QTD_PAGINA INT = 1000
		DECLARE @PAGINA INT = 0
		
		EXEC PROC_CHAT_GRUPO_DIA @IDCONTA,@DATAINICIO,@DATAFIM,@IDGRUPO,@ATENDENTE,@QTD_PAGINA,@PAGINA
*/
ALTER PROC PROC_CHAT_GRUPO_DIA 
(	
	 @IDCONTA  INT 
	,@DATAINICIO DATETIME
	,@DATAFIM DATETIME
	,@IDGRUPO VARCHAR(MAX)
	,@ATENDENTE VARCHAR(MAX)
	,@QTD_PAGINA INT 
	,@PAGINA INT 
)

AS 
	BEGIN
			
			SET @ATENDENTE =  '0,' + @ATENDENTE
			DECLARE @TIMEZONE VARCHAR(MAX)
			SELECT @TIMEZONE = T.UTCOFFSET FROM CONTA (NOLOCK) JOIN TIMEZONES T (NOLOCK) ON T.TIMEZONEID = CONTA.IDTIMEZONE WHERE CONTA.Id = @IDCONTA
			DECLARE @HOUR INT =  SUBSTRING(@TIMEZONE, 1, 3)

			SET @DATAINICIO = DATEADD(HOUR,(@HOUR * -1) ,@DATAINICIO)
			PRINT @DATAINICIO

			DECLARE @TABLE TABLE(IDGRUPO INT,DATACRIACAO DATE,ENTRANTES INT,RECEBIDOS INT,ABONDONADOS INT,TEMPO_MEDIO_ABONDONO INT,OFFILINE INT,TEMPO_MEDIO_ESPERA INT,TEMPO_MEDIO_ATENDIMENTO INT,ATENDIMENTO_ATE_30_SEG INT)
			DECLARE @TABLE1 TABLE(IDGRUPO INT,DATACRIACAO DATE,ENTRANTES INT,RECEBIDOS INT,ABONDONADOS INT,TEMPO_MEDIO_ABONDONO INT,OFFILINE INT,TEMPO_MEDIO_ESPERA INT,TEMPO_MEDIO_ATENDIMENTO INT, ATENDIMENTO_ATE_30_SEG INT)

			INSERT INTO @TABLE
			EXEC GETALLDAYSBETWEENTWODATE @DATAINICIO,@DATAFIM,@IDGRUPO,@TIMEZONE
			
			INSERT INTO @TABLE1
			SELECT
				C.IDGRUPOCHAT IDGRUPO,
				CAST(DATEADD(HOUR,@HOUR,C.DATACRIACAO) AS DATE) DATACRIACAO,
				SUM(1) ENTRANTES,
				SUM((CASE WHEN ISNULL(C.IDPROPRIETARIO,0) <> 0 THEN 1 ELSE 0 END)) RECEBIDOS,
				SUM((CASE WHEN ISNULL(C.IDPROPRIETARIO,0) = 0 THEN 1 ELSE 0 END)) ABONDONADOS,	
				AVG((CASE WHEN ISNULL(C.IDPROPRIETARIO,0) = 0 THEN DATEDIFF(SECOND,DATEADD(HOUR,@HOUR,C.DATACRIACAO),ISNULL(DATEADD(HOUR,@HOUR,C.DATACONCLUSAO),DATEADD(HOUR,@HOUR,C.DATACRIACAO))) ELSE 0 END)) TEMPO_MEDIO_ABONDONO,
				SUM((CASE WHEN C.IDSTATUS = 6 THEN 1 ELSE 0 END)) OFFILINE,
				AVG(DATEDIFF(SECOND,DATEADD(HOUR,@HOUR,C.DATACRIACAO),COALESCE(DATEADD(HOUR,@HOUR,C.DATAINICIO), DATEADD(HOUR,@HOUR,C.DATACONCLUSAO), DATEADD(HOUR,@HOUR,C.DATAALTERACAO)))) TEMPO_MEDIO_ESPERA,
				AVG((CASE WHEN ISNULL(C.IDPROPRIETARIO,0) <> 0 THEN DATEDIFF(SECOND,DATEADD(HOUR,@HOUR,C.DATAINICIO),ISNULL(DATEADD(HOUR,@HOUR,C.DATACONCLUSAO),DATEADD(HOUR,@HOUR,C.DATAINICIO))) ELSE 0 END)) TEMPO_MEDIO_ATENDIMENTO,
				SUM((CASE WHEN ISNULL(C.IDPROPRIETARIO,0) <> 0 AND DATEDIFF(SECOND,DATEADD(HOUR,@HOUR,C.DATACRIACAO),ISNULL(DATEADD(HOUR,@HOUR,C.DATAINICIO),DATEADD(HOUR,@HOUR,C.DATACRIACAO))) <= 30 THEN 1 ELSE 0 END)) ATENDIMENTO_ATE_30_SEG
			FROM CHAT C(NOLOCK)
			WHERE 
				C.IDCONTA = @IDCONTA 
				AND C.DATACRIACAO BETWEEN @DATAINICIO AND @DATAFIM
				AND C.IDGRUPOCHAT IN(SELECT INTVALUE FROM [DBO].[CSVTOINT](@IDGRUPO)) 
				AND ISNULL(C.IDPROPRIETARIO,0) IN(SELECT INTVALUE FROM [DBO].[CSVTOINT](@ATENDENTE)) 					
			GROUP BY C.IDGRUPOCHAT, CAST(DATEADD(HOUR,@HOUR,C.DATACRIACAO) AS DATE)

			UPDATE
			    A
			SET
				A.ENTRANTES = B.ENTRANTES,
				A.RECEBIDOS = B.RECEBIDOS,
				A.ABONDONADOS = B.ABONDONADOS,
				A.TEMPO_MEDIO_ABONDONO = B.TEMPO_MEDIO_ABONDONO,
				A.OFFILINE = B.OFFILINE,
				A.TEMPO_MEDIO_ESPERA = B.TEMPO_MEDIO_ESPERA,
				A.TEMPO_MEDIO_ATENDIMENTO = B.TEMPO_MEDIO_ATENDIMENTO,
				A.ATENDIMENTO_ATE_30_SEG = B.ATENDIMENTO_ATE_30_SEG,
				A.DATACRIACAO = B.DATACRIACAO
			FROM @TABLE AS A INNER JOIN @TABLE1 AS B ON A.IDGRUPO = B.IDGRUPO AND A.DATACRIACAO = B.DATACRIACAO
		
		  ;WITH CHAT_TEMP AS (SELECT *,ROW_NUMBER() OVER(ORDER BY DATACRIACAO DESC) AS ROWNUMBER
				FROM (	
						SELECT * FROM @TABLE
					 ) AS TEMPO)

			SELECT TOP (@QTD_PAGINA) 
				Grupo = (SELECT GG.NOME FROM GRUPOCHAT GG (NOLOCK) WHERE GG.ID = IDGRUPO),
				FORMAT(DATACRIACAO,'d', 'pt-BR' ) [Data de Criação],
				ENTRANTES [Quantidades de chats recebidos],
				RECEBIDOS [Quantidade de chat atendidos],
				ROUND(CAST(((CAST(RECEBIDOS AS DECIMAL)/CAST((CASE WHEN ENTRANTES = 0 THEN 1 ELSE ENTRANTES END) AS DECIMAL)) * 100) AS DECIMAL),2)[%de chat recebidos x atendidos],
				ABONDONADOS [Chat perdidos],
				ROUND(CAST(((CAST(ABONDONADOS AS DECIMAL)/CAST((CASE WHEN ENTRANTES = 0 THEN 1 ELSE ENTRANTES END) AS DECIMAL)) * 100) AS decimal),2)[%Taxa de perdido],
				CONVERT(VARCHAR, DATEADD(SECOND, ISNULL(TEMPO_MEDIO_ABONDONO,0), 0), 108)[Tempo médio de perdido],
				OFFILINE [chat fora do horário de atendimento],
				CONVERT(VARCHAR, DATEADD(SECOND, ISNULL(TEMPO_MEDIO_ESPERA,0), 0), 108) [Tempo Médio de espera para ser atendido],
				CONVERT(VARCHAR, DATEADD(SECOND, ISNULL(TEMPO_MEDIO_ATENDIMENTO,0), 0), 108) [Tempo Médio de Atendimento],
				CAST((CAST(ATENDIMENTO_ATE_30_SEG AS DECIMAL)/CAST((CASE WHEN RECEBIDOS = 0 THEN 1 ELSE RECEBIDOS END) AS DECIMAL) * 100) AS DECIMAL) [%Nível de serviço]
				--Total =(SELECT COUNT(1)FROM CHAT_TEMP)
			FROM CHAT_TEMP
			WHERE ROWNUMBER > (@PAGINA * @QTD_PAGINA)
			ORDER BY Grupo,DATACRIACAO DESC
		END
	
	